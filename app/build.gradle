apply plugin: 'com.android.application'
apply plugin: 'jacoco'
if (!rootProject.plugins.hasPlugin("org.sonarqube")) {
	println("sonarcube not yet applied")
	apply plugin: 'org.sonarqube'
}

android {
	signingConfigs {
		defaultConfig {
			keyAlias 'CheapListKey'
			keyPassword 'cheaplistkeypassword'
			storeFile file('..//keystore/android.jks')
			storePassword 'cheaplistkeystorepassword'
		}
	}
	compileSdkVersion compileSdk
	buildToolsVersion buildTools
	defaultConfig {
		applicationId "hu.tvarga.cheaplist"
		minSdkVersion minSdk
		targetSdkVersion targetSdk
		versionCode 1
		versionName project.version

		multiDexEnabled true
		vectorDrawables.useSupportLibrary = true
		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
	}
	testOptions {
		unitTests.all {
			jacoco {
				includeNoLocationClasses = true
			}
		}
		// Starting with Robolectric 3.3 there is now tighter integration with the tool chain,
		// where the build system processes and merges your resources.
		unitTests {
			includeAndroidResources = true
		}
	}
	buildTypes {
		debug {
			testCoverageEnabled true
		}
		release {
			minifyEnabled false
			shrinkResources false
			zipAlignEnabled true
			debuggable false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.defaultConfig
		}
	}
}

jacoco {
	toolVersion = "0.7.4+"
}


configurations { codacy }
repositories {
	maven { url "https://jitpack.io" }
	maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

dependencies {
	codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'

	implementation(project(path: ':lib')) {
		exclude group: 'com.google.firebase'
	}
	implementation "com.facebook.android:facebook-login:4.28.0"
	implementation "com.pavelsikun:materialdesignicons:1.0"
	implementation "com.android.support:cardview-v7:$supportLibraryVersion"
	implementation "com.android.support:design:$supportLibraryVersion"
	implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
	implementation "com.google.firebase:firebase-core:$firebaseVersion"
	implementation "com.google.firebase:firebase-auth:$firebaseVersion"
	implementation "com.google.firebase:firebase-config:$firebaseVersion"
	implementation "com.google.firebase:firebase-ads:$firebaseVersion"
	implementation "com.google.firebase:firebase-crash:$firebaseVersion"
	implementation "com.google.firebase:firebase-firestore:$firebaseVersion"
	implementation "com.firebaseui:firebase-ui-auth:$firebaseUIVersion"
	implementation "com.firebaseui:firebase-ui-database:$firebaseUIVersion"
	implementation "com.firebase:firebase-jobdispatcher:0.8.3"
	implementation "com.google.dagger:dagger:$daggerVersion"
	implementation "com.google.dagger:dagger-android:$daggerVersion"
	implementation "com.google.dagger:dagger-android-support:$daggerVersion"
	implementation "com.jakewharton.timber:timber:4.5.1"
	implementation "com.jakewharton:butterknife:8.8.1"
	implementation "com.github.bumptech.glide:glide:$glideVersion"
	implementation ("com.github.bumptech.glide:recyclerview-integration:$glideVersion") {
		// Excludes the support library because it's already included by Glide.
		transitive = false
	}
	implementation "org.greenrobot:eventbus:3.1.0-RC"
	debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
	releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

	testImplementation "junit:junit:4.12"
	testImplementation "org.mockito:mockito-core:$mockitoVersion"

	annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"
	annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
	annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
	androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	}
	androidTestImplementation 'com.azimolabs.conditionwatcher:conditionwatcher:0.2'
}

task jacocoMergedTestReport(type: JacocoReport) {
	FileCollection unitTestCoverageFile = files("$buildDir/jacoco/testDebugUnitTest.exec")

	FileCollection coverageFiles = getAndroidTestCoverageFiles() + unitTestCoverageFile

	inputs.files(coverageFiles)
	outputs.dir("$buildDir/reports/jacoco/jacocoMergedTestReport/html")

	setSourceDirectories(files(["$buildDir/../src/main/java/hu"]))
	setClassDirectories(fileTree(
			dir: "$buildDir/intermediates/classes/debug/hu",
			excludes: getCoverageExcludes()))

	setExecutionData(coverageFiles)

	reports {
		xml.enabled true
		csv.enabled false
		html.enabled true
	}
}

def getCoverageExcludes(String format) {
	def excludes = ['**/Manifest*.*',
					'**/BuildConfig.*',
					'**/R.class',
					'**/R$*.class'
	]
	if ("Sonar".equals(format)) {
		return String.join(",", excludes)
	} else {
		return excludes
	}
}

apply plugin: 'com.google.gms.google-services'

def getAndroidTestCoverageFiles() {
	FileCollection androidTestCoverageFiles = fileTree("$buildDir/outputs/code-coverage/connected") {
		include "**/*.ec"
	}
	return androidTestCoverageFiles
}

static def gitBranch() {
	def branch = ""
	def proc = "git rev-parse --abbrev-ref HEAD".execute()
	proc.in.eachLine { line -> branch = line }
	proc.err.eachLine { line -> println line }
	proc.waitFor()
	branch
}

sonarqube {
	properties {
		property "sonar.login", System.getenv('CHEAPLIST_CALEBZOR_GITHUB_SONAR_KEY')
		property "sonar.branch.name", gitBranch()
		property "sonar.tests", "src/test/java"
		property "sonar.java.binaries", "${buildDir}/intermediates/classes/debug"
		property "sonar.java.test.binaries", "${buildDir}/intermediates/classes/test/debug"
		property "sonar.jacoco.reportPaths", "${buildDir}/jacoco/testDebugUnitTest.exec"
		property "sonar.jacoco.itReportPath", getAndroidTestCoverageFiles()
		// NOTE: By default the java source set and not the android source set is used
		property "sonar.sources", android.sourceSets.main.java.srcDirs

		property "sonar.junit.reportsPath", "${buildDir}/test-results/testDebugUnitTest"

		// sonar lint plugin
		//
		// ATTENTION: this file must match the chosen build flavor in the
		// CI build configurations, otherwise lint issues will not be visible in the sonar
		// report. CI Build Configs:
		//  - Android CI -> Sonar preview report generation
		//  - Android Master Sonar ->  SonarQube
		property "sonar.android.lint.report", "${buildDir}/reports/lint-results-debug.xml"
	}
}

// if you get: com.codacy Error: Missing option --projectToken
// then:
// this should be set as environment variable in the CI
// set CODACY_PROJECT_TOKEN=Project API Token
// token from: https://www.codacy.com/app/calebzor/CheapList/settings/integrations

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoMergedTestReport) {
	main = "com.codacy.CodacyCoverageReporter"
	classpath = configurations.codacy
	args = [
			"-l",
			"Java",
			"-r",
			"${buildDir}/reports/jacoco/jacocoMergedTestReport/jacocoMergedTestReport.xml"
	]
}